#include "iostream.bah"
#include "http.bah"
#include "string.bah"
#include "path.bah"
#include "markdown.bah"
#include "thread.bah"

const BASE_PATH = "./pages"

COOLDOWN int*


parsePath(p cpstring) string {
    path = string(p)
    if path.compare("/") {
        path.append("index.html")
    }

    path.prepend(BASE_PATH)
    return path
}

server(serv http_server*, connfd int) {

    req = http_request(connfd)
    resp = http_response(connfd)

    resp.setHeader("Content-Type", "text/html")


    path = parsePath(req.path)

    *COOLDOWN = time(0)

    if path.compare("/ping") {
        resp.write("ok")
        resp.send()
        return
    }

    fs = fileMap{}
    page cpstring
    if isInside(BASE_PATH, path.str()) == false {
        resp.write("stop that")
        resp.send()
        return
    }
    if path.hasPrefix("./pages/doc/") {
        header = fs.open("./pages/doc/header.html")
        fs.close()
        resp.write(header)

        path.append(".md")
        mdf = fs.open(path.str())

        if fs.isValid() == 0 {
            mdf = "
# 404
## Page not found.
You can go to the [syntax](/doc/syntax) page to access the documentation.           
"
        }
        
        md = parseMarkdown(mdf)
        mdhtml = md.html()
        resp.write(mdhtml)

        fs.close()
        footer = fs.open("./pages/doc/footer.html")
        footerS = string(footer)
        footerS.replace("{isapp}", "true")
        fs.close()
        resp.write(footerS.str())
        
    } else {
        page = fs.open(path.str())
        fs.close()
        resp.write(page)
    }



    resp.send()
}

locateChrome() cpstring {
    paths = []cpstring{
        "/usr/bin/google-chrome-stable",
        "/usr/bin/google-chrome",
        "/usr/bin/chromium",
        "/usr/bin/chromium-browser",
        "/snap/bin/chromium"
        }

    i=0; for i < len(paths) {
        p = paths[i]
        if fileExists(p) {
            return p
        }
        i = i + 1
    }

    cmd = command("zenity --error --title \"Missing chromium\" --text \"Could not find chrome/chromium on this machine.\"")
    cmd.run()
    return ""
}

createChrome(port int) {
    cmdS = locateChrome() + " --app=http://localhost:" + intToStr(port) + "/doc/syntax"
    cmd = command(cmdS)
    cmd.run()
}

checkCooldown(id int) {
    for 1 == 1 {
        sleep(1)
        s = time(0) - *COOLDOWN
        if s > 2 {
            exit(0)
        }
    }
}

#cLib "g"

main(args []cpstring) {
    COOLDOWN = sharedMemory(0)
    *COOLDOWN = time(0)
    
    tcd = thread{}
    tcd.handle = checkCooldown
    tcd.create()
    
    port = 49152
    for port <= 65535 {
        if isPortAvailable(port) {
            break
        } else if port == 65535 {
            panic("webapp: no available port.")
        }
        port = port + 1
    }
    
    s = http_server{}
    s.port = port
    s.handle = server

    createChrome(port)

    s.listenAndServe()
}