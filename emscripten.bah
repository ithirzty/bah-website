#import "iostream.bah"
#import "exec.bah"
#import "rand.bah"

makeRandName() cpstring {
    name = []char

    i=0; for i < 20, i++ {
        if i % 2 == 0 {
            name[i] = <char>randomInRange(65, 90)
        } else {
            name[i] = <char>randomInRange(97, 122)
        }
    }
    name[21] = null

    return arrAsStr(name)
}

struct fileExec {
    js: cpstring
    error: cpstring
    folder: cpstring
    success: bool = true
}

deleteFiles(path cpstring, cleanup bool) {
    println("Removing...")
    files = listFiles(path)
    i=0; for i < len(files), i++ {
        removeFile(path+"/"+files[i])
    }
    removeFile(path)

    if cleanup == true {
        folders = listFiles("./tmp")
        currTime = getTimeUnix() / 1000000000
        i=0; for i < len(folders), i++ {
            if fileExists(folders[i]) == false {
                continue
            }
            lm = getLastModified(folders[i]+"/main.bah")
            if currTime - lm > 60 {
                deleteFiles(folders[i], false)
            }
        }
    }


}


sanitizeResp(a []char) []char {
    resp = []char
    i=0; for i < len(a), i++ {
        //is control char
        if a[i] == <char>0x1b {
            for i < len(a), i++ {
                if a[i] == 'm' {
                    break
                }
            }
            continue
        }
        resp[len(resp)] = a[i]
    }
    return resp
}

buildFile(f cpstring) fileExec* {
    randName = makeRandName()
    folder = "tmp/"+randName
    mkdir(folder, noCheck(S_IRWXU))

    fs = fileStream{}
    fs.open(folder+"/main.bah", "w")
    if fs.isValid() == false {
        panic("could not create file")
    }
    fs.writeFile("backtrace(arr ptr, l int) int {
        return 0
    }
    "+f)
    fs.close()

    fe = new fileExec{
        js: folder+"/main.js"
        folder: folder
        error: ""
    }
    print("[Bah]")
    cmd = command("bah "+folder+"/main.bah -rcp -c")
    err = cmd.runBytes()
    fe.error = arrToStr(sanitizeResp(err))

    fs.open(folder+"/main.c", "r")
    f = fs.readContent()
    f = "#include <stdlib.h>
    #include <string.h>\n"+f
    fs.close()
    fs.open(folder+"/main.c", "w")
    fs.writeFile(f)
    fs.close()

    print("[emcc]")
    cmd = command("/usr/lib/emscripten/emcc "+folder+"/main.c -w -o "+folder+"/main.js")
    err = cmd.runBytes()


    print("ok!")
    return fe
}