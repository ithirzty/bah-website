#include "iostream.bah"
#include "http.bah"
#include "string.bah"
#include "path.bah"
#include "flags.bah"
#include "markdown.bah"

const BASE_PATH = "./pages"


parsePath(p cpstring) string {
    path = string(p)
    if path.compare("/") {
        path.append("index.html")
    }

    path.prepend(BASE_PATH)
    return path
}

serveFile(p cpstring, req http_request, r http_response) bool {
    fs = fileStream{}
    fs.open(p, "rb")
    if fs.isValid() == 0 {
        return false
    }
    size = fs.getSize()
    
    startStr = string(req.getHeader("range"))
    start = 0
    r.write("")

    sp = string(p)

    if sp.hasSuffix(".mp4") {
        r.setHeader("content-type", "video/mp4")
        r.setHeader("Access-Control-Allow-Origin", "*")
    }

    if startStr.length != 0 {
        startStr.trimLeft(6)
        startStr.trimRight(1)
        start = strToInt(startStr.str())
        r.setHeader("accept-range", "bytes")
        r.setHeader("content-range", "bytes "+intToStr(start)+"-"+intToStr(size-1)+"/"+intToStr(size))
        r.code = HTTP_PARTIAL_CONTENT
        fs.setPos(start)
        size = size - start
    }

    r.setHeader("content-length", intToStr(size))
    r.send()


    incr = 48000
    offset=0; for offset < size {
        bytes = []char
        realIncr = incr
        if offset + realIncr > size {
            realIncr = size - offset
        }
        allocateArray(bytes, realIncr)
        i=0; for i < realIncr {
            bytes[i] = fs.getChar()
            i = i + 1
        }
        r.flush(bytes)
        offset = offset + realIncr
    }

    fs.close()
    return true
}

server(serv http_server*, connfd int) {

    req = http_request(connfd)
    resp = http_response(connfd)

    resp.setHeader("Content-Type", "text/html")

    path = parsePath(req.path)

    fs = fileMap{}
    page cpstring
    if path.hasPrefix("./pages/doc/") {
        header = fs.open("./pages/doc/header.html")
        fs.close()
        resp.write(header)

        path.append(".md")
        mdf = fs.open(path.str())

        if fs.isValid() == 0 {
            mdf = "
# 404
## Page not found.
You can go to the [syntax](/doc/syntax) page to access the documentation.           
"
        }
        
        md = parseMarkdown(mdf)
        mdhtml = md.html()
        resp.write(mdhtml)

        fs.close()
        footer = fs.open("./pages/doc/footer.html")
        footerS = string(footer)
        footerS.replace("{isapp}", "false")
        fs.close()
        resp.write(footerS.str())
        
    } else {
        serveFile(path.str(), req, resp)
    }



    resp.send()
}

s http_server

stopServer() {
    s.stop()
}

main(args []cpstring) int {
    flags = flags{}
    flags.addInt("port", "The port on which the server should listen.")
    flags.parse(args)

    s = http_server{}
    if flags.isSet("port") {
        s.port = flags.getInt("port")
    } else {
        println("-port argument not passsed, defaulting to port 8080.")
        s.port = 8080
    }
    s.handle = server
    signal(noCheck(SIGINT), &stopServer)
    s.listenAndServe()
    return 0
}